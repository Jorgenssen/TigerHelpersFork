plugins {
    id "java"
    id "maven-publish"
    id "com.diffplug.spotless" version "6.25.0"
}

// Update as needed
ext.wpilibVersion = "2025.3.1"

repositories {
    mavenCentral()
    maven {
        url = "https://frcmaven.wpi.edu/release"
    }
    maven {
        url = "https://maven.pkg.github.com/yourusername/TigerHelpers"
        credentials {
            username = System.getenv("GITHUB_USERNAME") ?: "yourusername"
            password = System.getenv("GITHUB_TOKEN") ?: "your-personal-access-token"
        }
    }
}

ext {
    wpilibVersion = "$wpilibVersion"
    groupId = "com.titaniumtigers4829"
    artifactId = "tigerhelpers"
    version = "1.0.3"
}

dependencies {
    implementation "edu.wpi.first.ntcore:ntcore-java:$wpilibVersion"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:$wpilibVersion"
    implementation "edu.wpi.first.wpilibj:wpilibj-java:$wpilibVersion"
    implementation "edu.wpi.first.wpimath:wpimath-java:$wpilibVersion"
    implementation "edu.wpi.first.wpinet:wpinet-java:$wpilibVersion"
    implementation "edu.wpi.first.wpiunits:wpiunits-java:$wpilibVersion"
}

spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Task to generate Javadocs
task generateDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = file("$projectDir/docs")
    classpath = sourceSets.main.compileClasspath
}

// Ensure compileJava depends on spotlessApply and generateDocs
compileJava.dependsOn "spotlessApply", "generateDocs"

// Configure publishing
publishing {
    repositories {
        maven {
            url = "https://maven.pkg.github.com/TitaniumTigers4829/TigerHelpers"
            credentials {
                username = project.findProperty("githubUsername") ?: "default"
                password = project.findProperty("githubToken") ?: "default"
            }         
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = "com.titaniumtigers4829"
            artifactId = "tigerhelpers"
            version = "1.0.4"
            from components.java
        }
    }
}
